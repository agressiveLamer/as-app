
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "org.openapi.generator" version "4.3.1"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'
mainClassName = 'ru.aservice.app.AsAppApplication'



allprojects {
    repositories {
        mavenCentral()
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.postgresql:postgresql:42.5.3'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'

    }

    group 'ru.aservice.app'

    compileJava {
        sourceCompatibility = 11
        targetCompatibility = 11
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 11
        targetCompatibility = 11
        options.encoding = 'UTF-8'
    }
}

configure(project(':app')) {

    dependencies {
        implementation(project(':api'))
    }

    jar {
        enabled = false
    }
}


configure(project(':api')) {
    apply plugin: "java"
    apply plugin: "org.openapi.generator"

    jar {
        archiveClassifier = ""
    }
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.dependsOn tasks.openApiGenerate

    dependencies {
        implementation 'io.springfox:springfox-oas:3.0.0'
        implementation 'io.springfox:springfox-swagger-ui:3.0.0'

        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        implementation 'org.openapitools:jackson-databind-nullable:0.2.2'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    }
    tasks {
        bootJar.enabled(false)
        bootRun.enabled(false)
    }

    openApiGenerate {
        generatorName = "spring"
        inputSpec = "$projectDir/src/main/resources/app.yaml".toString()
        outputDir = "$buildDir/generated".toString()
        apiPackage = "ru.aservice.app.api"
        invokerPackage = "ru.aservice.app.api"
        modelPackage = "ru.aservice.app.api.model"
        library = "spring-boot"
        version = "${rootProject.version}"
        group = "ru.aservice.app"
        configOptions = [
                dateLibrary                : "java8",
                skipDefaultInterface       : "true",
                interfaceOnly              : "true",
                useTags                    : "true",
                oas3                       : "true",
                legacyDiscriminatorBehavior: "false"
        ]
    }

    sourceSets {
        main {
            java {
                srcDir(files("${openApiGenerate.outputDir.get()}/src/main/java"))
            }
        }
    }

}
